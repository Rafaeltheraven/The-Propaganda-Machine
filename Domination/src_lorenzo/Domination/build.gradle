allprojects {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

apply plugin: 'com.android.application'

dependencies {
    implementation project(':AndroidME')
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-games:17.0.0'
}

android {
    compileOptions.encoding = 'cp1252'
    compileSdkVersion 23
    buildToolsVersion '27.0.3'
    // this to to avoid error from httpcore-4.1.2.jar and httpcore-4.1.2.jar having the same file
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', 'src_android', 'src_mapstore']
            resources.srcDirs = ['src', 'res_android']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    // this is to avoid the @TargetApi missing on methods giving errors.
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 4
    }
}

// same as ant tasks "setup.android"
task copyAssets(type: Copy) {
    from('game/Domination') {
        include 'maps/**'
        include 'help/**'
    }
    from('src/META-INF') {
        include 'services/**'
    }
    from 'gpl.txt'
    from 'ChangeLog.txt'
    into 'assets'
}
project.preBuild.dependsOn(copyAssets)

